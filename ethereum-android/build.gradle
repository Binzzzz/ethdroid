apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = '0.1.' + (new Date()).format('yyyyMMdd')

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.sqli.ethereum-java:ethereum-java-core:0.1.20161207'
}

repositories { maven { url "http://dl.bintray.com/sqli-nantes/ethereum-java" } }

def siteUrl = 'https://github.com/sqli-nantes/ethereum-android/ethereum-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/sqli-nantes/ethereum-android/ethereum-android.git'   // Git repository URL
group = "com.sqli"                                          // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com.sqli:ethereum-android'
                description = 'Description of your project HERE' // TODO
                url 'https://github.com/sqli-nantes/ethereum-android/ethereum-android'

                // Set your license
                licenses {
                    license {
                        name 'MIT'
                    }
                }
                scm {
                    connection 'https://github.com/sqli-nantes/ethereum-android/ethereum-android.git'
                    developerConnection 'https://github.com/sqli-nantes/ethereum-android/ethereum-android.git'
                    url 'https://github.com/sqli-nantes/ethereum-android/ethereum-android/'
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = project.hasProperty('BINTRAY_USER_ETHAND') ? project.property('BINTRAY_USER_ETHAND') : System.getenv('BINTRAY_USER_ETHAND')
    key = project.hasProperty('BINTRAY_APIKEY_ETHAND') ? project.property('BINTRAY_APIKEY_ETHAND') : System.getenv('BINTRAY_APIKEY_ETHAND')

    configurations = ['archives']
    pkg {
        repo = "ethereum-android"
        // it is the name that appears in bintray when logged
        name = "ethereum-android"
        userOrg = 'sqli-nantes'
        websiteUrl = 'https://github.com/sqli-nantes/ethereum-android/ethereum-android/'
        vcsUrl = 'https://github.com/sqli-nantes/ethereum-android/ethereum-android.git'
        licenses = ["MIT"]
        publish = true
        override = true
        version {
            name = '0.1.' + (new Date()).format('yyyyMMdd')
            desc = 'Ethereum-Android library'
            vcsTag = '0.1.' +(new Date()).format('yyyyMMdd')
        }
    }
}

